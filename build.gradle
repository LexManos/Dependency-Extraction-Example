buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

minecraft {
    version = project.forgeVersion
    runDir = "run"
    mappings = project.mcpVersion
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

task modJar(type: Jar) {
    from(sourceSets.main.output) {
        exclude 'io/paleocrafter/depextraction/core/**'
    }
    manifest {
        // Strictly speaking not required (right now)
        attributes 'Maven-Version': "${project.group}:${project.archivesBaseName}:${project.version}:mod"
    }
    classifier 'mod'
}

reobf {
    modJar { mappingType = 'SEARGE' }
}

jar {
    include 'io/paleocrafter/depextraction/core/**'
    from(modJar.archivePath.absolutePath) {
        include '*'
    }
    manifest {
        attributes 'FMLCorePlugin': "io.paleocrafter.depextraction.core.DepExtractionCore"
        attributes 'ContainedDeps': modJar.archivePath.name
    }
    dependsOn reobfModJar
}